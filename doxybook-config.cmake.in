@PACKAGE_INIT@

# How doxybook installation was built
set(DOXYBOOK_BUILT_SHARED "@BUILD_SHARED_LIBS@")
set(DOXYBOOK_BUILT_CXX_COMPILER_ID "@CMAKE_CXX_COMPILER_ID@")
set(DOXYBOOK_BUILT_CXX_COMPILER_VERSION "@CMAKE_CXX_COMPILER_VERSION@")

# Check if it matches the current toolchain
if (NOT CMAKE_CXX_COMPILER_ID STREQUAL DOXYBOOK_BUILT_CXX_COMPILER_ID)
    message(WARNING "This installation of doxybook was built with ${DOXYBOOK_BUILT_CXX_COMPILER_ID}: "
                    "Current compiler is ${CMAKE_CXX_COMPILER_ID}.")
endif()

# Paths
set_and_check(DOXYBOOK_INSTALL_DIR "@PACKAGE_CMAKE_INSTALL_LIBDIR@")
set_and_check(DOXYBOOK_INCLUDE_DIR "@PACKAGE_INCLUDE_INSTALL_DIR@")
set_and_check(DOXYBOOK_LIB_DIR     "@PACKAGE_LIB_INSTALL_DIR@")

# Find dependencies
include(CMakeFindDependencyMacro)
list(APPEND CMAKE_MODULE_PATH ${DOXYBOOK_CONFIG_INSTALL_DIR})
list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}")
list(APPEND CMAKE_MODULE_PATH "${DOXYBOOK_INCLUDE_DIR}")
list(APPEND CMAKE_MODULE_PATH "${DOXYBOOK_INSTALL_DIR}")

find_dependency(Threads)

set(DOXYBOOK_INSTALL_HAD_FMT @fmt_FOUND@)
set(DOXYBOOK_INSTALL_FETCHED_FMT @fmt_FETCHED@)
if (DOXYBOOK_INSTALL_HAD_FMT AND NOT DOXYBOOK_INSTALL_FETCHED_FMT)
    find_dependency(fmt)
endif()

set(DOXYBOOK_INSTALL_HAD_SPDLOG @spdlog_FOUND@)
set(DOXYBOOK_INSTALL_FETCHED_SPDLOG @spdlog_FETCHED@)
if (DOXYBOOK_INSTALL_HAD_SPDLOG AND NOT DOXYBOOK_INSTALL_FETCHED_SPDLOG)
    find_dependency(spdlog)
endif()

set(DOXYBOOK_INSTALL_HAD_NLOHMANN_JSON @nlohmann_json_FOUND@)
set(DOXYBOOK_INSTALL_FETCHED_NLOHMANN_JSON @nlohmann_json_FETCHED@)
if (DOXYBOOK_INSTALL_HAD_NLOHMANN_JSON AND NOT DOXYBOOK_INSTALL_FETCHED_NLOHMANN_JSON)
    find_dependency(nlohmann_json)
endif()

set(DOXYBOOK_INSTALL_HAD_INJA @inja_FOUND@)
set(DOXYBOOK_INSTALL_FETCHED_INJA @inja_FETCHED@)
if (DOXYBOOK_INSTALL_HAD_INJA AND NOT DOXYBOOK_INSTALL_FETCHED_INJA)
    find_dependency(inja)
endif()

set(DOXYBOOK_INSTALL_HAD_CXXOPTS @cxxopts_FOUND@)
set(DOXYBOOK_INSTALL_FETCHED_CXXOPTS @cxxopts_FETCHED@)
if (DOXYBOOK_INSTALL_HAD_CXXOPTS AND NOT DOXYBOOK_INSTALL_FETCHED_CXXOPTS)
    find_dependency(cxxopts)
endif()

if (MSVC)
    set(DOXYBOOK_INSTALL_HAD_DIRENT @dirent_FOUND@)
    set(DOXYBOOK_INSTALL_FETCHED_DIRENT @dirent_FETCHED@)
    if (DOXYBOOK_INSTALL_HAD_DIRENT AND NOT DOXYBOOK_INSTALL_FETCHED_DIRENT)
        find_dependency(dirent)
    endif()
endif()

list(POP_BACK CMAKE_MODULE_PATH)

# Create imported targets
include("${CMAKE_CURRENT_LIST_DIR}/doxybook-targets.cmake")
check_required_components(doxybook)
